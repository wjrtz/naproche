start: element*

element: directive
       | environment
       | sentence

directive: BRACKET_OPEN directive_name directive_arg* BRACKET_CLOSE
directive_name: "/"? word
directive_arg: path | /[^\s\]]+/

path: "\\path{" /[^}]+/ "}"

environment: BEGIN "{" env_name "}" optional_arg? element* END "{" env_name "}"

BEGIN.2: "\\begin"
END.2: "\\end"

env_name: /[a-zA-Z*]+/
optional_arg: "[" /[^\]]+/ "]"

sentence: atom+ DOT

atom: math | latex_cmd | word | other_symbol | punctuation

math: INLINE_MATH | DISPLAY_MATH
INLINE_MATH: /\$[^$]+\$/
DISPLAY_MATH: /\\\[(.|\n)+?\\\]/ | /\$\$(.|\n)+?\$\$/

latex_cmd: "\\" /[a-zA-Z]+/

word: /[a-zA-Z0-9_]+/
other_symbol: /[^\s\w$.\[\]\\{}]+/ | BACKSLASH | BRACE_OPEN | BRACE_CLOSE | BRACKET_OPEN | BRACKET_CLOSE
punctuation: "," | ":" | ";"

DOT: "."

BACKSLASH: "\\"
BRACE_OPEN: "{"
BRACE_CLOSE: "}"
BRACKET_OPEN: "["
BRACKET_CLOSE: "]"

COMMENT: "%" /[^\n]*/
%import common.WS
%ignore WS
%ignore COMMENT
